plugins {
    id 'java'
    id 'application'
    id("com.github.johnrengelman.shadow") version "7.1.0"
    id("com.palantir.docker") version '0.30.0'
}

group = "org.kiwiproject.elucidation"
version = "5.0.0"

repositories {
    mavenCentral()
    mavenLocal()
}

mainClassName = "org.kiwiproject.elucidation.App"

ext {
    // Runtime dependency versions
    dropwizardVersion = '2.1.2'
    elucidationVersion = '5.0.0'
    sqlliteVersion = '3.39.3.0'

    // Provided dependency versions
    lombokVersion = '1.18.24'

    // Test dependency versions
    junitVersion = '5.9.0'
    assertjVersion = '3.23.1'
    mockitoVersion = '4.8.0'
}

dependencies {
    implementation "org.kiwiproject:elucidation-bundle:${elucidationVersion}"
    implementation "io.dropwizard:dropwizard-core:${dropwizardVersion}"
    implementation "io.dropwizard:dropwizard-db:${dropwizardVersion}"
    implementation "io.dropwizard:dropwizard-jdbi3:${dropwizardVersion}"
    implementation "io.dropwizard:dropwizard-migrations:${dropwizardVersion}"
    implementation "org.xerial:sqlite-jdbc:${sqlliteVersion}"

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testImplementation "io.dropwizard:dropwizard-testing:${dropwizardVersion}"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
}

test {
    // Use junit platform for unit tests
    useJUnitPlatform()
}

shadowJar {
    archiveFileName.set('elucidation.jar')
    mergeServiceFiles()
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

docker {
    dependsOn shadowJar
    name 'kiwiproject/elucidation-server:latest'
    files tasks.shadowJar.outputs, 'config.yml'
}
