plugins {
    id 'java'
    id 'application'
    id("com.github.johnrengelman.shadow") version "5.2.0"
    id("com.palantir.docker") version '0.25.0'
}

group = "com.fortitudetec.elucidation"
version = "2.1.0"

repositories {
    mavenCentral()
}

mainClassName = "com.fortitudetec.elucidation.App"

ext {
    // Runtime dependency versions
    dropwizardVersion = '1.3.9'
    elucidationVersion = '2.1.0'
    sqlliteVersion = '3.30.1'

    // Provided dependency versions
    lombokVersion = '1.18.12'

    // Test dependency versions
    junitVersion = '5.6.1'
    assertjVersion = '3.15.0'
    mockitoVersion = '3.3.3'
}

dependencies {
    implementation "com.fortitudetec:elucidation-bundle:${elucidationVersion}"
    implementation "io.dropwizard:dropwizard-core:${dropwizardVersion}"
    implementation "io.dropwizard:dropwizard-db:${dropwizardVersion}"
    implementation "io.dropwizard:dropwizard-jdbi3:${dropwizardVersion}"
    implementation "io.dropwizard:dropwizard-migrations:${dropwizardVersion}"
    implementation "org.xerial:sqlite-jdbc:${sqlliteVersion}"

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testImplementation "io.dropwizard:dropwizard-testing:${dropwizardVersion}"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
}

test {
    // Use junit platform for unit tests
    useJUnitPlatform()
}

shadowJar {
    archiveName 'elucidation.jar'
    mergeServiceFiles()
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

docker {
    dependsOn shadowJar
    name 'fortitudetec/elucidation-server:latest'
    files tasks.shadowJar.outputs, 'config.yml'
}
